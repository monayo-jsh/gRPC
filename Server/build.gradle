plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.protobuf' version '0.9.4' // grpc proto type 생성 lib
}

group = 'com.exmple'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'net.devh:grpc-server-spring-boot-starter:2.13.0.RELEASE'

    // grpc proto type 생성 lib
    implementation 'io.grpc:grpc-protobuf:1.56.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.grpc:grpc-netty-shaded:1.56.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// grpc proto type 생성 설정
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.56.0"
        }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

// grpc proto type 생성 설정
sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

// grpc proto type 생성 설정
tasks.withType(ProcessResources) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}